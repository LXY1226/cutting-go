#include "textflag.h"

// modify from runtime/sys_linux_amd64.s
// g_* macro generated from go 1.18.3 windows/amd64
TEXT 路WallTime(SB),NOSPLIT,$16-12
    // We don't know how much stack space the VDSO code will need,
	// so switch to g0.
	// In particular, a kernel configured with CONFIG_OPTIMIZE_INLINING=n
	// and hardening can use a full page of stack space in gettime_sym
	// due to stack probes inserted to avoid stack/heap collisions.
	// See issue #20427.
	MOVQ	SP, R12	// Save old SP; R12 unchanged by C code.

    MOVQ	0x30(R14), BX   // g_m(R14), BX     // BX unchanged by C code.

    CMPQ	R14, 0xc0(BX)   // R14, m_curg(BX)  // Only switch if on curg.
    JNE	noswitchWall

    MOVQ	0x0(BX), DX     // m_g0(BX), DX
    MOVQ	0x38(DX), SP    // (g_sched+gobuf_sp)(DX), SP	// Set SP to g0 stack

noswitchWall:
    SUBQ	$16, SP		// Space for results
    ANDQ	$~15, SP	// Align for C code

    MOVL	$0, DI // CLOCK_REALTIME
    LEAQ	0(SP), SI
    MOVQ	runtime路vdsoClockgettimeSym(SB), AX
    CALL	AX

    MOVQ	0(SP), AX	// sec
    MOVQ	8(SP), DX	// nsec
    MOVQ	R12, SP		// Restore real SP

    MOVQ	AX, sec+0(FP)
    MOVL	DX, nsec+8(FP)
    RET

TEXT 路WallTimeNano(SB),NOSPLIT,$16-12
    // We don't know how much stack space the VDSO code will need,
	// so switch to g0.
	// In particular, a kernel configured with CONFIG_OPTIMIZE_INLINING=n
	// and hardening can use a full page of stack space in gettime_sym
	// due to stack probes inserted to avoid stack/heap collisions.
	// See issue #20427.
	MOVQ	SP, R12	// Save old SP; R12 unchanged by C code.

    MOVQ	0x30(R14), BX   // g_m(R14), BX     // BX unchanged by C code.

    CMPQ	R14, 0xc0(BX)   // R14, m_curg(BX)  // Only switch if on curg.
    JNE	noswitchNano

    MOVQ	0x0(BX), DX     // m_g0(BX), DX
    MOVQ	0x38(DX), SP    // (g_sched+gobuf_sp)(DX), SP	// Set SP to g0 stack

noswitchNano:
    SUBQ	$16, SP		// Space for results
    ANDQ	$~15, SP	// Align for C code

    MOVL	$0, DI // CLOCK_REALTIME
    LEAQ	0(SP), SI
    MOVQ	runtime路vdsoClockgettimeSym(SB), AX
    CALL	AX

    MOVQ	0(SP), AX	// sec
    MOVQ	8(SP), DX	// nsec
    MOVQ	R12, SP		// Restore real SP

	// sec is in AX, nsec in DX
	// return nsec in AX
	IMULQ	$1000000000, AX
	ADDQ	DX, AX
	MOVQ	AX, ret+0(FP)
	RET
